[[structure]]
[role=group,includesConcepts="structure:*"]
== Structure

=== Concepts

[[structure:RootPackage]]
[source,cypher,role=concept]
.The main artifact contains the `Root` package of the libary "org.junit".
----
MATCH
  (artifact:Main:Artifact)-[:CONTAINS]->(root:Package)
WHERE
  root.fqn="org.junit"
SET
  root:Root
RETURN
  root as RootPackage
----

[[structure:Component]]
[source,cypher,role=concept,requiresConcepts="structure:RootPackage"]
.Every package that is a direct child of the root package is a `Component`.
----
MATCH
  (root:Root:Package)-[:CONTAINS]->(component:Package)
SET
  component:Component
RETURN
  component.name as Component
ORDER BY
  Component
----

[[structure:ComponentDependency]]
[source,cypher,role=concept,requiresConcepts="structure:Component"]
.A component depends on another component (`DEPENDS_ON_COMPONENT`) if exists at least one dependency of contained Java types.
----
MATCH
  (c1:Component)-[:CONTAINS*]->(t1:Type),
  (c2:Component)-[:CONTAINS*]->(t2:Type),
  (t1)-[:DEPENDS_ON]->(t2)
WHERE
  c1 <> c2
WITH
  c1, c2, count(t2) as weight
MERGE
  (c1)-[d:DEPENDS_ON_COMPONENT]->(c2)
SET
  d.weight = weight
RETURN
  c1 as Component, c2 as Dependency, weight as Weight
ORDER BY
  Component, Weight desc
----

=== Reports

[[structure:ComponentDependencyDiagram]]
[source,cypher,role=concept,requiresConcepts="structure:ComponentDependency",reportType="plantuml-component-diagram"]
.Creates a diagram about components and their dependencies.
----
MATCH
  (c:Component)
OPTIONAL MATCH
  (c)-[d:DEPENDS_ON_COMPONENT]->(:Component)
RETURN
  *
----